
0 ' MYSTERY OF SILVER MOUNTAIN.
1 '   - DIRECT TYPE IN FROM JIM GERRIE'S MC10 VERSION.
2 '   - FIXED UP 64 CHAR ISSUES.
3 '   - REQUIRES FURTHER WORK.
4 '
5 'VERSION : 1.0 RELEASED.
 

10 CLEAR600:CLS:EL=31:NO=88:NV=57:G=28:GOSUB3380
30 GOSUB4400
40 LL=0
50 GOSUB3310
60 P$=X$(VAL(LEFT$(D$,1)))+" "+Y$(VAL(MID$(D$,2,1)))+" "
70 J$=R$+". "+"YOU ARE "+P$+RIGHT$(D$,LEN(D$)-2)+" ":GOSUB4830
80 GOSUB3330:J$=""
90 FORI=1TOG-1
100 READO$
110 P$=Y$(VAL(LEFT$(O$,1))):GOSUB3350
120 IFA(3,I)=0ANDA(2,I)=R,J$=J$+" "+P$+" "+O$+","
130 NEXTI
140 IFR=29ANDA(3,48)=0,J$=J$+" GRARGS FEASTING,"
150 IFR=29ANDA(3,48)=1,J$=J$+" A SLEEPING GRARG,"
160 IFR=12ORR=22,J$=J$+" A PONY,"
170 IFR=64,J$=J$+" A HERMIT,"
180 IFR=18ANDA(1,18)=1100000,J$=J$+" AN OAK DOOR,"
190 IFR=59ANDA(3,68)=1,J$=J$+" OGBAN (DEAD),"
200 IFJ$<>"",J$=", YOU CAN SEE"+J$
210 J$=J$+" AND YOU CAN GO "
220 GOSUB4830:PRINT" ";
230 IFMID$(STR$(A(1,R)),3,1)="1",PRINT"N,";
231 IFMID$(STR$(A(1,R)),4,1)="1",PRINT"S,";
232 IFMID$(STR$(A(1,R)),5,1)="1",PRINT"E,";
233 IFMID$(STR$(A(1,R)),6,1)="1",PRINT"W,";
234 IFMID$(STR$(A(1,R)),7,1)="1",PRINT"U,";
235 IFMID$(STR$(A(1,R)),8,1)="1",PRINT"D,";
240 PRINTCHR$(8):PRINT
250 R$="PARDON?":PRINT"================================";
260 PRINT"WHAT WILL YOU DO NOW"
270 INPUTI$
280 IFI$="SAVE"ORI$="SAVE GAME",GOSUB4630
290 V$="":T$="":VB=0:B=0
300 FORI=1TOLEN(I$)
310 IFMID$(I$,I,1)=" "ANDV$="",V$=LEFT$(I$,I-1)


320 IFMID$(I$,I+1,1)<>" "ANDV$<>"",T$=MID$(I$,I+1,LEN(I$)-1)
321 IFMID$(I$,I+1,1)<>" "ANDV$<>"",I=LEN(I$)
330 NEXTI:IFT$="",V$=I$
340 IFLEN(V$)<3,V$=V$+"O":GOTO340
350 IFV$="PLAY",V$="BLO"
360 U$=LEFT$(V$,3)
370 FORI=1TONV:IFMID$(B$,I*3-2,3)=U$,VB=I:I=NV
380 NEXTI:A(3,36)=0
390 GOSUB3330
400 FORI=1TONO:READO$:IFI<=G,GOSUB3350
410 IFT$=O$,B=I:I=NO
420 NEXTI
430 IFB=0ANDA(3,36)=0ANDT$>"",T$=T$+"S":A(3,36)=1:GOTO390
440 IFVB=0,VB=NV+1
450 IFT$="",R$="I NEED TWO WORDS"
460 IFVB>NV,R$="TRY SOMETHING ELSE"
470 IFVB>NVANDB=0,R$="YOU CANNOT "+I$
480 IFB>G ORB=0,510
490 IFVB=8ORVB=9ORVB=14ORVB=17ORVB=44ORVB>54,510
500 IFVB<NVANDA(2,B)<>0,R$="YOU DO NOT HAVE THE "+T$:GOTO30


510 IFR=56ANDA(3,35)=0ANDVB<>37ANDVB<>53,R$=X1$+" HAS GOT YOU!"
511 IFR=56ANDA(3,35)=0ANDVB<>37ANDVB<>53,GOTO30

520 IFVB=44ORVB=47ORVB=19ORVB=57ORVB=49,540
530 IFR=48ANDA(3,63)=0,R$=X9$:GOTO30
540 H=VAL(STR$(R)+STR$(B))
550 IFINT((VB-1)/13)+1=1,GOTO560
551 IFINT((VB-1)/13)+1=2,GOTO580
552 IFINT((VB-1)/13)+1=3,GOTO600
553 IFINT((VB-1)/13)+1=4,GOTO620
554 IFINT((VB-1)/13)+1=5,GOTO640


560 IFVB=1ORVB=2ORVB=3ORVB=4ORVB=5ORVB=6,GOSUB800
561 IFVB=7 ,GOSUB1220
562 IFVB=8 OR VB=9,GOSUB1290
563 IFVB=10 OR VB=11 ,GOSUB1470
564 IFVB=12, GOSUB1750
565 IFVB=13, GOSUB1890

570 GOTO650






580 IFVB-13=1,GOSUB1960
581 IFVB-13=2,GOSUB1980
582 IFVB-13=3,GOSUB2010
583 IFVB-13=4,GOSUB2050
584 IFVB-13=5,GOSUB2870
585 IFVB-13=6,GOSUB2120
586 IFVB-13=7,GOSUB2220
587 IFVB-13=8,GOSUB2310
588 IFVB-13=9,GOSUB2380
589 IFVB-13=10,GOSUB2420
590 IFVB-13=11,GOSUB2450
591 IFVB-13=12,GOSUB2470
592 IFVB-13=13,GOSUB2520
593 GOTO650
600 IFVB-26=1,GOSUB2550
601 IFVB-26=2,GOSUB2580
602 IFVB-26=3,GOSUB2610
603 IFVB-26=4,GOSUB2650
604 IFVB-26=5,GOSUB2670
605 IFVB-26=6,GOSUB2700
606 IFVB-26=7,GOSUB2720
607 IFVB-26=8,GOSUB2730
608 IFVB-26=9,GOSUB2830
609 IFVB-26=10,GOSUB2800
610 IFVB-26=11,GOSUB2870
611 IFVB-26=12,GOSUB2730
612 IFVB-26=13,GOSUB2920
613 GOTO650

620 IFVB-39=1,GOSUB2950
621 IFVB-39=2,GOSUB2990
622 IFVB-39=3,GOSUB3010
623 IFVB-39=4,GOSUB3050
624 IFVB-39=5,GOSUB3070
625 IFVB-39=6,GOSUB2310
626 IFVB-39=7,GOSUB2990
627 IFVB-39=8,GOSUB3070
628 IFVB-39=9,GOSUB3130
629 IFVB-39=10,GOSUB3120
630 IFVB-39=11,GOSUB3190
631 IFVB-39=12,GOSUB1470
632 IFVB-39=13,GOSUB3100

633 GOTO650

640 IFVB-52=1,GOSUB2870
641 IFVB-52=2,GOSUB3150
642 IFVB-52=3,GOSUB1290
643 IFVB-52=4,GOSUB1290
644 IFVB-52=5,GOSUB3170
645 IFVB-52=6,GOSUB3200


650 IFA(3,62)=1,730


660 IFR=41,A(3,67)=A(3,67)+1:IFA(3,67)=10,A(3,56)=1
661 IFA(3,67)=10,R$="YOU SANK!"
670 IFR=56ANDA(3,35)=0ANDA(2,10)<>0,R$=X1$+" GETS YOU!"
671 IFR=56ANDA(3,35)=0ANDA(2,10)<>0,A(3,56)=1
680 IFA(3,56)=0,30
690 GOSUB4400:PRINTR$
700 PRINT"YOU HAVE FAILED IN YOUR QUEST!"
710 PRINT:PRINT"BUT YOU ARE GRANTED ANOTHER TRY"
720 GOSUB3360:RUN
730 GOSUB4400
740 PRINT"HOOOOORRRRRAAAAAYYYYY!"
750 PRINT
760 PRINT"YOU HAVE SUCCEEDED IN YOUR"
770 PRINT"QUEST AND BROUGHT PEACE TO"
780 PRINT"THE LAND"
790 END
800 D=VB
810 IFD=5,D=1
820 IFD=6,D=3
830 IFNOT((R=75ANDD=2)OR(R=76ANDD=4))ORA(3,64)=1,850
840 R$="B USPMM TUPQT ZPV DSPTTJOH":GOSUB4260:RETURN
850 IFA(3,64)=1,A(3,64)=0
860 IFA(3,51)=1ORA(3,29)=1,900
870 IFA(3,55)=1,A(3,56)=1:R$="GRARGS HAVE GOT YOU!":RETURN
880 IFR=29ANDA(3,48)=0,R$="GRARGS WILL SEE YOU!":RETURN
890 IFR=73ORR=42ORR=9ORR=10,R$=X3$:A(3,55)=1:RETURN



900 IFA(2,8)=0AND((R=52ANDD=2)OR(R=31ANDD<>3)),R$="THE BOAT IS "
901 IFA(2,8)=0AND((R=52ANDD=2)OR(R=31ANDD<>3)),R$=R$+"TOO HEAVY"
902 IFA(2,8)=0AND((R=52ANDD=2)OR(R=31ANDD<>3)),RETURN
910 IFA(2,8)<>0AND((R=52ANDD=4)OR(R=31ANDD=3)),R$="YOU CANNOT "
911 IFA(2,8)<>0AND((R=52ANDD=4)OR(R=31ANDD=3)),R$=R$+"SWIM"
912 IFA(2,8)<>0AND((R=52ANDD=4)OR(R=31ANDD=3)),RETURN
920 IFR=52ANDA(2,8)=0ANDD=4ANDA(3,30)=0,R$="NO POWER!":RETURN
930 IFR=41ANDD=3ANDA(3,31)=0,R$="UIF CPBU JT TJOLJOH!":GOSUB4260
931 IFR=41ANDD=3ANDA(3,31)=0,RETURN
940 IFR=33ANDD=1ANDA(3,32)=0,R$="OGBAN'S BOAR BLOCKS YOUR PATH"
941 IFR=33ANDD=1ANDA(3,32)=0,RETURN
950 IF((R=3ANDD=2)OR(R=4ANDD=4))ANDA(3,45)=0,R$=X5$:RETURN
960 IFR=35ANDA(2,13)<>R,R$="THE ICE IS BREAKING!":RETURN
970 IFR=5AND(D=2ORD=4),GOSUB4310
980 IFR=4ANDD=4,R$="PASSAGE IS TOO STEEP":RETURN
990 IFR=7ANDD=2ANDA(3,46)=0,R$="A HUGE HOUND BARS YOUR WAY"
991 IFR=7ANDD=2ANDA(3,46)=0,RETURN
1000 IF(R=38ORR=37)ANDA(3,50)=0,R$="JU JT UPP EBSL"
1001 IF(R=38ORR=37)ANDA(3,50)=0,GOSUB4260:RETURN
1010 IFR=49ANDD=2ANDA(3,54)=0,R$="MYSTERIOUS FORCES HOLD YOU "
1011 IFR=49ANDD=2ANDA(3,54)=0,R$=R$+"BACK":RETURN
1020 IFR=49ANDD=3ANDA(3,68)=0,R$="YOU MET OGBAN!!!"
1021 IFR=49ANDD=3ANDA(3,68)=0,A(3,56)=1:RETURN


1030 IFR=38ANDA(3,65)=0,R$="RATS NIBBLE YOUR ANKLES":RETURN


1040 IFR=58AND(D=1ORD=4)ANDA(3,66)=0,R$="YOU GET CAUGHT IN THE "
1041 IFR=58AND(D=1ORD=4)ANDA(3,66)=0,R$=R$+"WEBS!":RETURN
1050 IFR=48ANDD=4ANDA(3,70)=0,R$="THE DOOR DOES NOT OPEN":RETURN
1060 IFR=40ANDA(3,47)=1,A(3,68)=1
1070 IFR=37ANDD=4ANDA(1,37)=1001100,R=67:R$="THE PASSAGE WAS "
1071 IFR=37ANDD=4ANDA(1,37)=1001100,R=67:R$=R$+"STEEP!":RETURN
1080 IFR=29ANDD=3,A(3,48)=1:A(3,20)=0
1090 IFR=8ANDD=2,A(3,46)=0
1100 OM=R


1120 Z1=MID$(STR$(A(1,OM)),3,1):Z2=MID$(STR$(A(1,OM)),7,1)
1121 IF(Z1="1"ORZ1="1")ANDD=1,R=R-10

1130 IFMID$(STR$(A(1,OM)),5,1)="1"ANDD=2,R=R+1
1140 Z1=MID$(STR$(A(1,OM)),4,1):Z2=MID$(STR$(A(1,OM)),8,1)="1")
1141 IF(Z1="1"ORZ2="1")ANDD=3,R=R+10
1150 IFMID$(STR$(A(1,OM)),6,1)="1"ANDD=4,R=R-1
1160 R$="OK"
1170 IFR=OM,R$="YOU CANNOT GO THAT WAY"
1180 IF((OM=75ANDD=2)OR(OM=76ANDD=4)),R$="OK. YOU CROSSED"
1190 IFA(3,29)=1,A(3,39)=A(3,39)+1
1200 IFA(3,39)>5ANDA(3,29)=1,R$="CPPUT IBWF XPSO PVU":GOSUB4260
1200 IFA(3,39)>5ANDA(3,29)=1,A(3,29)=0:A(2,3)=81

1210 RETURN
1220 GOSUB3330:R$="OK":A(3,49)=0
1230 PRINT"YOU HAVE ";
1240 FORI=1TOG:READO$:GOSUB3350
1241 IFI=1ANDA(2,1)=0ANDA(3,44)=1,O$="COIN"


1250 IFI=GANDA(2,5)=0,1270
1260 IFA(2,I)=0,PRINTO$;",";:A(3,49)=1
1270 NEXTI:IFA(3,49)=0,PRINT"NOTHING"
1280 PRINT:GOSUB3360:RETURN
1290 IFH=6577,R$="HOW?":RETURN
1300 IFH=4177ORH=5177,B=16:GOSUB2380:RETURN
1310 IFB=38,R$="TOO HEAVY!":RETURN
1320 IFB=4ANDA(3,43)=0,R$="IT IS FIRMLY NAILED ON!":RETURN
1330 CO=0:FORI=1TOG-1:IFA(2,I)=0,CO=CO+1
1340 NEXTI:IFCO>13,R$="YOU CANNOT CARRY ANY MORE":RETURN
1350 IFB>G,R$="YOU CANNOT GET THE "+T$:RETURN
1360 IFB=0,RETURN
1370 IFA(2,B)<>R,R$="IT IS NOT HERE!"
1380 IFA(3,B)=1,R$="WHAT "+T$+"?"
1390 IFA(2,B)=0,R$="YOU ALREADY HAVE IT"
1400 IFA(2,B)=RANDA(3,B)=0,A(2,B)=0:R$="YOU HAVE THE "+T$
1410 IFB=28,A(2,5)=81
1420 IFB=5,A(2,28)=0
1430 IFA(2,4)=0ANDA(2,12)=0ANDA(2,15)=0,A(3,54)=1
1440 IFB=8ANDA(3,30)=1,A(2,2)=0
1450 IFB=2,A(3,30)=0
1460 RETURN
1470 R$="YOU SEE WHAT YOU MIGHT EXPECT!"
1480 IFB>0,R$="NOTHING SPECIAL"
1490 IFB=46ORB=88,GOSUB2550
1500 IFH=8076,R$="IT IS EMPTY"
1510 IFH=8080,R$="AHA!":A(3,1)=0
1520 IFH=7029,R$="OK":A(3,2)=0
1530 IFB=20,R$="NBUDIFT JO QPDLFU":GOSUB4260:A(2,26)=0
1540 IFH=1648,R$="THERE ARE SOME LETTERS '"+G$(2)+"'"
1550 IFH=7432,R$="UIFZ BSF BQQMF USFFT":GOSUB4260:A(3,5)=0
1560 IFH=2134ORH=2187,R$="OK":A(3,16)=0
1570 IFB=35,R$="IT IS FISHY!":A(3,17)=0
1580 IFH=3438,R$="OK":A(3,22)=0
1590 IFH=242,R$="A FADED INSCRIPTION"
1600 IF(H=1443ORH=1485)ANDA(3,33)=0,R$="B HMJNNFSJOH GSPN UIF"
1601 IF(H=1443ORH=1485)ANDA(3,33)=0,R$=R$+" UIF EFQUIT"
1602 IF(H=1443ORH=1485)ANDA(3,33)=0,GOSUB4260

1610 IF(H=1443ORH=1485)ANDA(3,33)=1,R$="SOMETHING HERE..."
1611 IF(H=1443ORH=1485)ANDA(3,33)=1,A(3,12)=0
1612 IF(H=1443ORH=1485)ANDA(3,33)=1,R$="SOMETHING HERE..."
1613 IF(H=1443ORH=1485)ANDA(3,33)=1,A(3,12)=0


1620 IFH=2479ORH=2444,R$="THERE IS A HANDLE"
1630 IFB=9,R$="UIF MBCFM SFBET 'QPJTPO'":GOSUB4260
1640 IFH=4055,GOSUB3290
1650 IFH=2969ANDA(3,48)=1,R$="VERY UGLY!"
1660 IFH=7158ORH=7186,R$="THERE ARE LOOSE BRICKS"
1670 IFR=49,R$="VERY INTERESTING!"
1680 IFB=52ORB=82ORB=81,R$="INTERESTING!"
1690 IFH=6978,R$="THERE IS A WOODEN DOOR"
1700 IFH=6970,R$="YOU FOUND SOMETHING":A(3,4)=0
1710 IFH=2066,R$="A LARGE CUPBOARD IN THE CORNER"
1720 IFH=6865,R$="THERE ARE NINE STONES"
1730 IFH=248,R$="B GBEFE XPSE - 'N S I T'":GOSUB4260
1740 RETURN
1750 IFR=64,R$="HE GIVES IT BACK!"
1760 IFH=6425,GOSUB3210
1770 IFR=75ORR=76,R$="HE DOES NOT WANT IT"
1780 IFB=62ANDA(3,44)=0,R$="YOU HAVE RUN OUT!"
1790 IF(H=7562ORH=7662)ANDA(3,44)>0ANDA(2,1)=0,R$="HE TAKES IT"
1791 IF(H=7562ORH=7662)ANDA(3,44)>0ANDA(2,1)=0,A(3,64)=1


1800 IFA(3,64)=1,A(3,44)=A(3,44)-1:IFA(3,44)=0,A(2,1)=81
1810 IFB=1,R$="HE TAKES THEM ALL!":A(2,1)=81:A(3,64)=1:A(3,44)=0
1820 IFH=2228ANDA(2,5)=81,R$=XB$+"NORTH":A(2,28)=81:R=12
1830 IF(H=2228ANDA(2,5)=0)ORH=225,R$=XB$+"NORTH":R=12
1840 IF(H=1228ANDA(2,5)=0)ORH=125,R$=XB$+"SOUTH":R=22
1850 IFR=7ORR=33,R$="HE EATS IT!":A(2,B)=81
1860 IFH=711,A(3,46)=1:R$="HE IS DISTRACTED"
1870 IFH=385ORH=3824,R$="THEY SCURRY AWAY":A(2,B)=81:A(3,65)=1
1880 RETURN
1890 R$="YOU SAID IT"
1900 IFB=84,R$="YOU MUST SAY THEM ONE BY ONE!":RETURN
1910 IFR<>47ORB<71ORB>75ORA(2,27)<>0,RETURN
1920 IFB=71ANDA(3,60)=0,R$=X7$:A(3,60)=1:RETURN
1930 IFB=72ANDA(3,60)=1ANDA(3,61)=0,R$=X8$:A(3,61)=1:RETURN
1940 IFB=(A(3,52)+73)ANDA(3,60)=1ANDA(3,61)=1,A(3,62)=1:RETURN
1950 R$="THE WRONG SACRED WORD!":A(3,56)=1:RETURN
1960 IFB=5ORB=10,GOSUB1290
1970 RETURN
1980 IFB=3,A(3,29)=1:R$="ZPV BSF JOWJTJCMF":A(3,55)=0:GOSUB4260
1990 IFB=20,A(3,51)=1:R$="ZPV BSF EJTHVJIFE":A(3,55)=0:GOSUB4260
2000 RETURN
2010 IFB=2ORB=14,R$="NOTHING TO TIE IT TO!"
2020 IFH=7214,R$="IT IS TIED":A(2,14)=72:A(3,53)=1
2030 IFH=722,R$="OK":A(3,40)=1:A(2,2)=72
2040 RETURN
2050 IFH=1547ANDA(3,38)=1,R$="ALL RIGHT":R=16
2060 IFB=14ORB=2,R$="NOT ATTACHED TO ANYTHING!"
2070 IFH=5414ANDA(2,14)=54,R$="YOU ARE AT THE TOP"
2080 IFH=7214ANDA(3,53)=1,R$="GOING DOWN":R=71
2090 IFH=722ANDA(3,40)=1,R=71:R$="IT IS TORN"
2091 IFH=722ANDA(3,40)=1,A(2,2)=81:A(3,40)=0
2100 IFH=7114ANDA(3,53)=1,A(2,14)=71:A(3,53)=0
2101 IFH=7114ANDA(3,53)=1,R$="IT FALLS DOWN - BUMP!"
2110 RETURN
2120 IFH=522,R$="OK":A(3,30)=1
2130 IFB=1ORB=62ORB=5ORB=28ORB=11ORB=24,GOSUB1750


2140 IFH=416,R$="ZPV IBWF LFQU BGMPBU":A(3,31)=1
2141 IFH=416,GOSUB4260:RETURN
2150 IFH=4116,R$="IT IS NOT BIG ENOUGH!":RETURN
2160 IFB=18ORB=7,GOSUB2470
2170 IFB=13,GOSUB2730
2180 IFB=19,GOSUB3070
2190 IFB=10,GOSUB2870
2200 IFB=16ORB=6,GOSUB2380
2210 RETURN
2220 IFB=76ORB=38,GOSUB1470
2230 IFH=2030,A(3,9)=0:R$="OK"
2240 IFH=6030,R$="OK":A(3,3)=0
2250 IFH=2444ORH=1870,R$="YOU ARE NOT STRONG ENOUGH"
2260 IFH=3756,R$="A PASSAGE!":A(1,37)=1001100
2270 IFH=5960,GOSUB3260
2280 IFH=6970,R$="IT FALLS OFF ITS HINGES"
2290 IFH=4870,R$="IT IS LOCKED"
2300 RETURN
2310 IFB>G,R$="IT DOES NOT BURN"
2320 IFB=26,R$="YOU LIT THEM"
2330 IFH=3826,R$="NOT BRIGHT ENOUGH"
2340 IF(B=23ORH=6970)ANDA(2,26)<>0,R$="OP NBUDIFT":GOSUB4260
2350 IFB=23ANDA(2,26)=0,R$="A BRIGHT "+V$:A(3,50)=1
2360 IFH=6970ANDA(2,26)=0,A(3,43)=1:R$="IT HAS TURNED TO ASHES"


2370 RETURN
2380 IF(B=16ORB=6)AND(R=41ORR=51),R$="YOU CAPSIZED!":A(3,56)=1
2390 IFH=6516ANDA(2,16)=0,R$="IT IS NOW FULL":A(3,34)=1
2400 IFH=656,R$="IT LEAKS OUT!"
2410 RETURN
2420 IFB<>22ORR<>15,R$="DOES NOT GROW!":RETURN
2430 R$="OK":A(3,37)=1
2440 RETURN
2450 IFB=22ANDA(3,37)=1ANDA(3,34)=1,R$=X2$:A(3,38)=1:GOSUB4260
2460 RETURN
2470 IFB=7ORB=18,R$="THWACK!"
2480 IFH=5818,R$="YOU CLEARED THE WEBS":A(3,66)=1
2490 IFH=187,R$="THE DOOR BROKE!":A(1,18)=1110000
2491 IFH=187,A(1,28)=1110000


2500 IFH=717,R$="YOU BROKE THROUGH":A(1,71)=1100000
2510 RETURN
2520 IFB=16,B=22:GOSUB2450
2530 IFH=499,R$="WHERE?"
2540 RETURN
2550 IFH=4337,VB=2:GOSUB800:RETURN
2560 IFR=36,R$="YOU FOUND SOMETHING":A(3,13)=0
2570 RETURN
2580 IFR=76,VB=4:GOSUB800:RETURN
2590 IFR=75,VB=2:GOSUB800
2600 RETURN
2610 IF(B=3ANDA(3,29)=1),R$="TAKEN OFF":A(3,29)=0
2620 IF(B=20ANDA(3,51)=1),R$="OK":A(3,51)=0
2630 IFB=36ORB=50,GOSUB2950
2640 RETURN
2650 IFH=3859ORH=3339ORH=1241ORH=2241ORH=751,R$="WITH WHAT?"
2660 RETURN
2670 IFH=2340,R$="IT GOES ROUND"
2680 IFH=2445,R$="UIF HBUFT PQFO, UIF QPPM FNQUJFT":A(3,33)=1
2681 IFH=2445,GOSUB4260



2690 RETURN
2700 IFR=14ORR=51,R$="YOU HAVE DROWNED":A(3,56)=1
2710 RETURN
2720 R$="HOW?":RETURN
2730 IFB=0ORB>G,RETURN
2740 A(2,B)=R:R$="DONE"
2750 IFH=418ORH=518,R$="YOU DROWNED!":A(3,56)=1
2760 IFB=8ANDA(3,30)=1,A(2,2)=R
2770 IFB=16ANDA(3,34)=1,R$="YOU LOST THE WATER!":A(3,34)=0
2780 IFB=2ANDA(3,30)=1,A(3,30)=0
2790 RETURN
2800 IFB=62ANDA(3,44)=0,R$="YOU DO NOT HAVE ANY"
2810 IFH=5762ANDA(2,1)=0ANDA(3,44)>0,GOSUB3230
2820 RETURN
2830 IFB=0ORB>G,RETURN
2840 R$="DID NOT GO FAR!":A(2,B)=R
2850 IFH=3317,R$="ZPV DBVHIU UIF CPBS":A(3,32)=1:GOSUB4260
2860 RETURN
2870 IFB=10,R$="B OJDF UVOF":GOSUB4260
2880 IFH=5233,R$="WHAT WITH?"
2890 IFB=83,R$="HOW, O MUSICAL ONE?"
2900 IFH=5610,A(3,35)=1:R$=X1$+" IS FREE!":A(1,56)=1110000
2910 RETURN
2920 IFB=0ORB>G,RETURN
2930 IFB=5ORB=24,R$="YUM YUM!":A(2,B)=81
2940 RETURN
2950 IFR=4ANDB=50,A(3,45)=1:R$="YOU REVEALED A STEEP PASSAGE"
2960 IFR=3ANDB=50,R$="YOU CANNOT MOVE RUBBLE FROM HERE"
2970 IFH=7136,R$="THEY ARE WEDGED IN!"
2980 RETURN
2990 IF(B=67ORB=68)ANDA(2,9)=0ANDR=49,R$="OK":A(3,47)=1
3000 RETURN
3010 IFR<>27ORB<>63,RETURN
3020 PRINT:PRINT"HOW MANY TIMES?":INPUTMR:
3021 IFMR=0,PRINT"A NUMBER":GOTO3020
3030 IFMR=A(3,42),R$="A ROCK DOOR OPENS":A(1,27)=1001100:
3031 RETURN
3040 R$="ZPV IBWF NJTUSFBUFE UIF CFMM!":A(3,56)=1:GOSUB4260
3041 RETURN

3050 IFH=5861,H=5818:GOSUB2470
3060 RETURN
3070 IF(H=4864ORH=4819)ANDA(2,19)=0,R$=X6$:A(3,63)=1:GOSUB4260
3080 IFB=27,GOSUB1290
3090 RETURN
3100 IFH=7549ORH=7649,R$="WHAT WITH?"
3110 IFB=1ORB=62,GOSUB1750
3120 RETURN
3130 IFH=4870ANDA(2,21)=0,R$="THE KEY TURNS!":A(3,70)=1
3140 RETURN
3150 IFH=1870,R$="HOW?"
3160 RETURN
3170 IFR=48,R$="HOW?"
3180 RETURN
3190 R$="ARE YOU THIRSTY?"
3200 RETURN


3210 R$="HE TAKES IT AND SAYS '"+STR$(A(3,42))
3211 R$=R$+" RINGS ARE NEEDED'":A(2,25)=81
3220 RETURN
3230 A(3,44)=A(3,44)-1:R$="A NUMBER APPEARS - "+STR$(A(3,41))
3240 IFA(3,44)=0,A(2,1)=81
3250 RETURN
3260 PRINT:R$="XIBU JT UIF DPEF":GOSUB4260:PRINTR$:INPUTCN
3270 R$="WRONG!":IFCN=A(3,41),R$="IT OPENS":A(3,21)=0
3280 RETURN
3290 T=R:R=A(3,A(3,52)+57):GOSUB3310:R=T
3300 R$=X4$+RIGHT$(D$,LEN(D$)-2):RETURN
3310 RESTORE:FORI=1TOR:READD$:NEXTI
3320 RETURN
3330 RESTORE:FORI=1TO80:READD$:NEXTI
3340 RETURN
3350 O$=RIGHT$(O$,LEN(O$)-1):RETURN
3360 INPUT"PRESS enter TO CONTINUE";Z$:RETURN
3380 DIMA(3,80),X$(6),Y$(6),G$(2)
3390 GOSUB3330
3400 FORI=1TONO:READT$:NEXTI
3410 FORI=1TO6:READX$(I),Y$(I):NEXTI


3420 B$="NOOEOOSOOWOOUOODOOINVGETTAKEXAREAGIVSAYPICWEATIECLIRIG"
3421 B$=B$+"USEOPE"
3430 B$=B$+"LIGFILPLAWATSWIEMPENTCROREMFEETURDIVBAILEATHRINSBLO"
3431 B$=B$+"DROEATMOV"
3440 B$=B$+"INTRINCUTHOLBURPOISHOUNLWITDRICOUPAYMAKBRESTEGATREF"
3450 X6$="ZPV SFGMFDUFE UIF XJABSET HMBSF! IF JT EFBE"
3460 X1$="THE GHOST OF THE GOBLIN GUARDIAN"
3470 X2$= "B MBSHF WJOF HSPXT JO TFDPOET!"
3480 X3$="A GRARG PATROL APPROACHES"
3490 X4$="MAGIC WORDS LIE AT THE CROSSROADS, THE FOUNTAIN AND "
3491 X4$="THE "


3500 X5$="A PILE OF RUBBLE BLOCKS YOUR PATH"
3510 X7$="THE MOUNTAIN RUMBLES!"
3520 X8$="TOWERS FALL DOWN!"
3530 X9$="THE WIZARD HAS YOU IN HIS GLARE"
3540 XB$="HE LEADS YOU "
3550 GOSUB4400:PRINT"DO YOU WANT TO"
3560 PRINT:PRINT"     1. START A NEW GAME"
3570 PRINT" OR  2. CONTINUE A SAVED GAME"
3580 PRINT:PRINT:PRINT"TYPE IN EITHER 1 OR 2"
3590 INPUTC:IFC<>1ANDC<>2,3580
3600 IFC=1,GOSUB4450
3610 IFC=2,GOSUB4600
3620 RETURN
3630 DATA11HALF-DUG GRAVE,12GOBLIN GRAVEYARD
3640 DATA11HOLLOW TOMB,23STALACTITES AND STALAGMITES
3650 DATA11MAZE OF TUNNELS,11VAULTED CAVERN
3660 DATA23HIGH GLASS GATES,12ENTRANCE HALL TO THE PALACE
3670 DATA31GRARG SENTRY POST,12GUARD ROOM
3680 DATA31MARSHY INLET,23RUSTY GATES
3690 DATA12GAMEKEEPER'S COTTAGE,31MISTY POOL
3700 DATA11HIGH-WALLED GARDEN,14INSCRIBED CAVERN
3710 DATA34ORNATE FOUNTAIN,11DANK CORRIDOR
3720 DATA12LONG GALLERY,12KITCHENS OF THE PALACE
3730 DATA34OLD KILN,44OVERGROWN TRACK
3740 DATA31DISUSED WATERWHEEL,33SLUICE GATES
3750 DATA11GAP BETWEEN SOME BOULDERS,41PERILOUS PATH
3760 DATA31SILVER BELL IN THE ROCK,12DUNGEONS OF THE PALACE
3770 DATA11BANQUETING HALL,42PALACE BATTLEMENTS
3780 DATA44ISLAND SHORE,31BEACHED KETCH
3790 DATA13BARREN COUNTRYSIDE,33SACKS ON THE UPPER FLOOR


3800 DATA46FROZEN POND,21MOUNTAIN HUT
3810 DATA31ROW OF CASKS,11WINE CELLAR
3820 DATA12HALL OF TAPESTRIES,11DUSTY LIBRARY
3830 DATA13ROUGH WATER,11PLOUGHED FIELD
3840 DATA55OUTSIDE A WINDMILL,42LOWER FLOOR OF THE MILL
3850 DATA44ICY PATH,41SCREE SLOPE
3860 DATA12SILVER CHAMBER,12WIZARD'S LAIR
3870 DATA11MOSAIC-FLOORED HALL,12SILVER THRONE ROOM
3880 DATA12MIDDLE OF THE LAKE,42EDGE OF AN ICY LAKE
3890 DATA41PITTED TRACK,41HIGH PINNACLE
3900 DATA55ABOVE A GLACIER,21HUGH FALLEN OAK
3910 DATA11TURRET ROOM WITH A SLOT MACHINE,11COBWEBBY ROOM
3920 DATA31SAFE IN OGBAN'S CHAMBER,31CUPBOARD IN A CORNER
3930 DATA11NARROW PASSAGE,16CAVE
3940 DATA11WOODMAN'S HUT,42SIDE OF A WOODED VALLEY
3950 DATA21STREAM IN A VALLEY BOTTOM,11DEEP DARK WOOD
3960 DATA11SHADY HOLLOW,34ANCIENT STONE CIRCLE
3970 DATA16STABLE,14ATTIC BEDROOM
3980 DATA11DAMP WELL BOTTOM,32TOP OF A DEEP WELL
3990 DATA31BURNT-OUT CAMPFIRE,16ORCHARD
4000 DATA62END OF A BRIDGE,62END OF A BRIDGE
4010 DATA61CROSSROADS,41WINDING ROAD
4020 DATA11VILLAGE OF RUSTIC HOUSES,11WHITE COTTAGE


4030 DATA3COINS,1SHEET,3BOOTS,1HORSESHOE,3APPLES,1BUCKET,4AXE
4031 DATA1BOAT,1PHIAL
4040 DATA3REEDS,1BONE,1SHIELD,3PLANKS,1ROPE,1RING,1JUG,1NET
4041 DATA1SWORD
4050 DATA1SILVER PLATE,1UNIFORM,1KEY,3SEEDS,1LAMP,3BREAD,1BROOCH
4051 DATA3MATCHES
4060 DATA2STONE OF DESTINY,4APPLE,BED,CUPBOARD,BRIDGE,TREES,SAIL
4061 DATAKILN
4070 DATAKETCH,BRICKS,WINDMILL,SACKS,OGBAN'S BOAR,WHEEL
4080 DATAPONY,GRAVESTONES,POOL,GATES,HANDLE,HUT,VINE
4081 DATAINSCRIPTIONS,TROLL,RUBBLE
4090 DATAHOUND,FOUNTAIN,CIRCLE,MOSAICS,BOOKS,CASKS,WELL,WALLS
4091 DATA RATS,SAFE
4100 DATACOBWEBS,COIN,BELL,UP SILVER PLATE,STONES,KITCHENS
4101 DATAGOBLET,WINE
4110 DATAGRARGS,DOOR,AWAKE,GUIDE,PROTECT,LEAD,HELP,CHEST,WATER
4120 DATASTABLES,SLUICE GATES,POT,STATUE,PINNACLE,MUSIC
4121 DATAMAGIC WORDS


4130 DATAMISTY POOL,WELL BOTTOM,OLD KILN,MOUNTAIN HUT
4140 DATAIN,A,NEAR,THE,BY,SOME,ON,AN,"","",AT,A SMALL


4150 DATA1001000,1011100,1001100,1001100,1001100,1011100,1011100
4151 DATA1011000,1001100,1010100
4160 DATA1010000,1100000,1011000,1010100,1010000,1100100,1100000
4161 DATA1100000,1011000,1110100
4170 DATA1110000,1001000,1110100,1100000,1111000,1001100,1000100
4171 DATA1010000,1110000,1100000
4180 DATA1111000,1000100,1110000,1000001,1111000,1010100,1001000
4181 DATA1100100,1110000,1010000
4190 DATA1110000,1001000,1111100,1000110,1000011,1110000,1001000
4191 DATA1010100,1111000,1100100
4200 DATA1101000,1001100,1110100,1001000,1100100,1010000,1001000
4201 DATA1101100,1100100,1010000
4210 DATA1011000,1010100,1111000,1001100,1010100,1101000,1001100
4211 DATA1011100,1010100,1100001
4220 DATA1000000,1001000,1101100,1001100,1101100,1001100,1001100
4221 DATA1101100,1101100,1000110
4230 DATA80,70,60,69,74,72,63,52,20,11,1,14,36,54,61,21,32,10,50
4240 DATA29,59,34,13,80,30,81,47,74
4250 DATA1,2,3,4,5,9,12,13,16,17,20,21,22
4260 Z$="":FORI=1TOLEN(R$)
4270 C$=MID$(R$,I,1):IFC$<"A",Z$=Z$+C$:GOTO4300
4280 C=ASC(C$)-1:IFC=64,C=90
4290 Z$=Z$+CHR$(C)
4300 NEXTI:R$=Z$:RETURN
4310 J$="SSSSSSSS":NG=0
4320 MP=D/2:GOSUB4400
4330 PRINT"YOU ARE LOST IN THE TUNNELS"
4340 PRINT"WHICH WAY? (N,S,W OR E)"
4350 IFNG>15,PRINT"(OR G TO GIVE UP!)"
4360 PRINT:INPUTW$:J$=RIGHT$(J$+RIGHT$(W$,1),8)
4370 IFW$="G",A(3,56)=1:RETURN
4380 IFJ$<>G$(MP),NG=NG+1:GOTO4320
4390 RETURN


4400 CLS:PRINT@39,"MYSTERY OF SILVER":PRINTTAB(12)"MOUNTAIN"
4401 PRINT"============================V1.0";:RETURN
4450 FORI=1TO80:READA(1,I):NEXTI
4460 FORI=1TOG:READA(2,I):NEXTI
4470 FORI=1TO13:READA:A(3,A)=1:NEXTI
4480 A(3,41)=INT(RND(0)*900)+100:A(3,42)=INT(RND(0)*3)+2
4490 A(3,44)=4:A(3,57)=68:A(3,58)=54:A(3,59)=15
4491 A(3,52)=INT(RND(0)*3)
4500 R=77:R$="GOOD LUCK ON YOUR QUEST!"
4510 G$(1)="":G$(2)="":FORI=1TO8
4520 F$=MID$(B$,1+INT(RND(0)*4)*3,1)
4530 G$(1)=G$(1)+F$
4540 IFF$="N",L$="S"
4550 IFF$="S",L$="N"
4560 IFF$="E",L$="W"
4570 IFF$="W",L$="E"
4580 G$(2)=L$+G$(2)


4590 NEXTI:FORI=1TO8:F$=MID$(G$(1),I,1):
4591 IFF$<>"",A(2,60+I)=ASC(F$)
4592 F$=MID$(G$(2),I,1):IFF$<>"",A(2,70+I)=ASC(F$)
4594 NEXTI:RETURN
4600 PRINT"PRESS PLAY":CLOAD,"SILVERDT":R=A(3,69):
4601 R$="OK. CARRY ON"
4602 G$(1)="":G$(2)="":FORI=1TO8:
4603 IFA(2,60+I)>31,G$(1)=G$(1)+CHR$(A(2,60+I))
4604 IFA(2,70+I)>31,G$(2)=G$(2)+CHR$(A(2,70+I))
4606 NEXTI:RETURN
4630 A(3,69)=R:PRINT"PRESS PLAY & RECORD":
4631 INPUT", HIT enter";I$:CSAVE,"SILVERDT":RETURN
4830 LS=1:LP=1
4840 FORI=1TOLEN(J$)
4850 IFMID$(J$,I,1)=" "ANDLL>EL,PRINTMID$(J$,LP,LS-LP):LL=I-LS
4851 IFMID$(J$,I,1)=" "ANDLL>EL,LP=LS+1
4860 IFMID$(J$,I,1)=" ",LS=I
4870 LL=LL+1:NEXT:PRINTMID$(J$,LP,LEN(J$)-LP);:RETURN
