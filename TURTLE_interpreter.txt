10 REM :  ENTER IN LOGO COMMANDS          IN LINE 310
15 REM :  U = UP
20 REM :  D = DOWN
25 REM :  T = TURN
30 REM :  M = MOVE
35 REM :  C = COLOUR !!
40 REM :
45 REM :  EG:
55 REM  CM$="D 2 C 140 M 85 T 160 M 4 C 200 M 3 C 90 T 120 M"
60 REM  CM$="D 6 ( 4 ( 2 C 60 M 60 T 4 C 60 M 240 T ) 180 T )"
70  CM$="D 3 ( 400 M 2 ( 120 T 200 M ) 2 ( 240 T 200 M ) 120 T )
100 REM
110 DIM C$(160),  CM$(160)
220 CLEAR200: SW=128:SH=48:XS=SW/1000:YS=(SH*4)/(1000*3)
250 REM === SET TURTLE START AT CENTER OF SCREEN POINTING UP, PP
260 SW=SW-1:SH=SH-1: TX=500:TY=SH/YS/2:TA=90:TP=0:PI=3.14159
300 REM COMMAND INTERPRETER
310 :REM'CM$="5 ( D 12 ( 210 M 5 ( 30 T 10 M ) ) U 100 T 150 M )
315 :REM'CM$="D 6 ( 4 ( 2 C 60 M 60 T 4 C 60 M 240 T ) 180 T )"
320 :REM'CM$="D 3 ( 400 M 2 ( 120 T 200 M ) 2 ( 240 T 200 M ) 120 T )
325 :REM'CM$="D 3 ( 200 M 2 ( 120 T 100 M ) 2 ( 240 T 100 M ) 120 T
330 CLS:PRINT"VZ LOGO":PRINT
331 PRINT "U - PEN UP"
332 PRINT "D - PEN DOWN"
333 PRINT "T - TURN DEGREES"
334 PRINT "M - MOVE FORWARD"
335 PRINT "C - COLOUR":PRINT:PRINT"HIT ANY KEY"
336 B$=INKEY$:A$=INKEY$:IFA$=""THEN336
354 PRINT:PRINT" EXAMPLE:":PRINT
356 PRINT "45 T = TURN 45 DEGREES"
357 PRINT "80 M = MOVE FWD 80 POINTS"
358 PRINT "3 C  = CHANGE COLOUR TO BLUE"
359 PRINT "U 50 M = PEN UP, MOVE FWD 50"
360 PRINT "D 20 M = PEN DOWN, MOVE FWD 20"
361 PRINT "1 C = GREEN      2 C = YELLOW"
362 PRINT "3 C = BLUE       4 C = RED"
363 INPUT "HIT ANY KEY";A$
364 PRINT "100 M 4 C = MOVE FORWARD 100 RED";
365 PRINT "10 T 80 M = TURN 10, MOVE FWD 80";:PRINT
370 PRINT"5 ( D 12 ( 210 M 5 ( 30 T 10 M ) ) U 100 T 150 M )"
373 PRINT"D 6 ( 4 ( 2 C 60 M 60 T 4 C 60 M 240 T ) 180 T )"
374 PRINT"D 3 ( 400 M 2 ( 120 T 200 M ) 2 ( 240 T 200 M ) 120 T
375 PRINT"D 3 ( 200 M 2 ( 120 T 100 M ) 2 ( 240 T 100 M ) 120 T"
380 PRINT:PRINT"ENTER IN A SEQUENCE...":INPUTCM$
381 CM$="-45TD18(10M10T)-80T18(10M10T)-80T24(15M10T)U-80T20M-55"
382 :REM'CM$=CM$+"T2(D18(5M-10T)U10T170M)U165T30MD5MU5T90MD5MU90T80"
383 :REM'CM$=CM$+"M5TD18(10M10T)"
490 MODE(1):IP=1:SP=0
492  POKE30862,82:POKE30863,121:POKE31058,243:POKE31059,201
493 X=USR(X)
500 REM === COMMAND INTERPRETER
520 C$=MID$(CM$,IP,1)
530 IFC$=" ",IP=IP+1:GOTO520
540 IFC$>="-"ANDC$<="9",V=VAL(MID$(CM$,IP)):GOSUB1200
550 IFC$="(",V=IP:SP=SP+1:SK(SP)=V
560 IFC$=")",GOSUB2010
570 IFC$="M",GOSUB2510
580 IFC$="T",GOSUB3010
590 IFC$="U",TP=0
600 IFC$="D",TP=1
605 IFC$="C",V=SK(SP):SP=SP-1:COLORV
610 IP=IP+1:IFIP>LEN(CM$),100
650 GOTO520
1000 REM === PUSH ONTO STACK
1010 SP=SP+1:SK(SP)=V:RETURN
1100 REM === POP FROM STACK
1110 V=SK(SP):SP=SP-1:RETURN
1200 REM
1205 SP=SP+1:SK(SP)=V
1210 IP=IP+1
1220 IF IP>LEN(CM$) THEN END
1230 C2$=MID$(CM$,IP,1)
1240 IF C2$>="-"ANDC2$<="9",1210
1250 IP=IP-1
1260 RETURN
1500 REM === PLOT A LINE
1510 X1%=X1:X2%=X2:Y1%=Y1:Y2%=Y2
1520 DX%=ABS(X2%-X1%):SX%=-1:IFX1%<X2%,SX%=1
1530 DY%=ABS(Y2%-Y1%):SY%=-1:IFY1%<Y2%,SY%=1
1540 ER%=-DY%:IFDX%>DY%,ER%=DX%
1550 ER%=ER%/2
1560 SET(X1%,Y1%):IFX1%=X2%ANDY1%=Y2%,RETURN
1590 E2%=ER%:IFE2%>-DX%,ER%=ER%-DY%:X1%=X1%+SX%
1600 IFX1%=X2%ANDY1%=Y2%,RETURN
1610 IFE2%<DY%,ER%=ER%+DX%:Y1%=Y1%+SY%
1620 IFX1%=X2%ANDY1%=Y2%,RETURN
1630 GOTO 1560
2000 REM === LOOP INSTRUCTION END
2010 V=SK(SP):SP=SP-1:BP=V:V=SK(SP):SP=SP-1:LR=V
2030 LR=LR-1:IFLR<1,RETURN
2050 V=LR:SP=SP+1:SK(SP)=V:V=BP:SP=SP+1:SK(SP)=V:IP=BP:RETURN
2500 REM === MOVE
2510 V=SK(SP):SP=SP-1
2520 LX=V*COS((360-TA)*PI/180):LY=V*SIN((360-TA)*PI/180)
2530 IFTP>0,X1=TX:Y1=TY:X2=X1+LX:Y2=Y1+LY:GOSUB4510
2540 TX=TX+LX:TY=TY+LY:RETURN
3000 REM TURN
3010 V=SK(SP):SP=SP-1:TA=TA+V
3030 IFTA<0,TA=TA+360:GOTO3030
3040 IFTA>=360,TA=TA-360:GOTO3040
3050 RETURN
4500 REM === DRAW LINE, CLIPPING IF NEEDED
4510 X1=X1*XS:X2=X2*XS:Y1=Y1*YS:Y2=Y2*YS
4520 X=X1:Y=Y1:GOSUB5010:C1=C
4530 X=X2:Y=Y2:GOSUB5010:C2=C
4540 IFC1=0ANDC2=0,GOSUB1510:RETURN
4550 IFC1ANDC2,RETURN
4560 C=C1:IFC=0,C=C2
4570 IFCAND1,X=X1+(X2-X1)*(SH-Y1)/(Y2-Y1):Y=SH:GOTO4610
4580 IFCAND2,X=X1+(X2-X1)*(0-Y1)/(Y2-Y1):Y=0:GOTO4610
4590 IFCAND4,Y=Y1+(Y2-Y1)*(SW-X1)/(X2-X1):X=SW:GOTO4610
4600 IFCAND8,Y=Y1+(Y2-Y1)*(0-X1)/(X2-X1):X=0:GOTO4610
4610 IFC=C1,X1=X:Y1=Y:GOTO4520
4620 X2=X:Y2=Y:GOTO4520
5000 REM === CALCULATE REGION CODE
5010 C=0:IFY>SH,C=COR1
5030 IFY<0,C=COR2
5040 IFX>SW,C=COR4
5050 IFX<0,C=COR8
5060 RETURN
